---
page_title: "Data Source k8sconnect_yaml_split - terraform-provider-k8sconnect"
subcategory: ""
description: |-
  Splits multi-document YAML content into individual manifests with stable, human-readable IDs. Supports inline content, file patterns, or kustomize builds.
---

# Data Source: k8sconnect_yaml_split

Splits multi-document YAML content into individual manifests with stable, human-readable IDs. Supports inline content, file patterns, or kustomize builds.

## Example Usage - Inline Content

<!-- runnable-test: yaml-split-inline -->
```terraform
data "k8sconnect_yaml_split" "resources" {
  content = <<-YAML
    apiVersion: v1
    kind: Namespace
    metadata:
      name: example
    ---
    apiVersion: v1
    kind: ConfigMap
    metadata:
      name: app-config
      namespace: example
    data:
      key: value
    ---
    apiVersion: v1
    kind: Secret
    metadata:
      name: app-secret
      namespace: example
    type: Opaque
    data:
      password: cGFzc3dvcmQ=
  YAML
}

resource "k8sconnect_object" "resources" {
  for_each = data.k8sconnect_yaml_split.resources.manifests

  yaml_body          = each.value
  cluster = local.cluster
}
```
<!-- /runnable-test -->

## Example Usage - File Pattern

```terraform
# Load all YAML files from a directory
data "k8sconnect_yaml_split" "all_manifests" {
  pattern = "${path.module}/manifests/*.yaml"
}

resource "k8sconnect_object" "all" {
  for_each = data.k8sconnect_yaml_split.all_manifests.manifests

  yaml_body          = each.value
  cluster = local.cluster
}
```

## Example Usage - Kustomize

```terraform
# Build kustomization and deploy all resources
data "k8sconnect_yaml_split" "app" {
  kustomize_path = "${path.module}/kustomize/overlays/production"
}

resource "k8sconnect_object" "app" {
  for_each = data.k8sconnect_yaml_split.app.manifests

  yaml_body = each.value
  cluster   = local.cluster
}
```

The `kustomize_path` option runs `kustomize build` on the specified directory, combining bases and overlays into final manifests.

See the [kustomize-basic](https://github.com/jmorris0x0/terraform-provider-k8sconnect/tree/main/examples/kustomize-basic) example for a complete working setup with base + overlay structure.

## Manifest IDs

The datasource generates stable IDs in the format:
- **Cluster-scoped**: `kind.name` (e.g., `namespace.production`, `clusterrole.admin`)
- **Namespaced**: `kind.namespace.name` (e.g., `deployment.default.nginx`, `service.app.api`)

This prevents resource recreation when manifests are reordered in the YAML file.

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `content` (String) Raw YAML content containing one or more Kubernetes manifests separated by '---'. Mutually exclusive with 'pattern' and 'kustomize_path'.
- `kustomize_path` (String) Path to a kustomization directory (containing kustomization.yaml). Runs 'kustomize build' and parses the output. Mutually exclusive with 'content' and 'pattern'.
- `pattern` (String) Glob pattern to match YAML files (e.g., './manifests/*.yaml', './configs/**/*.yml'). Supports recursive patterns. Mutually exclusive with 'content' and 'kustomize_path'.

### Read-Only

- `id` (String) Data source identifier based on input content hash.
- `manifests` (Map of String) Map of stable manifest IDs to YAML content. IDs follow the format 'kind.name' (cluster-scoped) or 'kind.namespace.name' (namespaced). Duplicate IDs will cause an error.
