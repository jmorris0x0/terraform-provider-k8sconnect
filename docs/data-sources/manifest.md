---
page_title: "Data Source k8sconnect_manifest - terraform-provider-k8sconnect"
subcategory: ""
description: |-
  Reads an existing Kubernetes resource from the cluster and makes its data available to Terraform configuration. Use this to reference cluster resources not managed by Terraform (e.g., cloud provider defaults, operator-created resources) or to access dynamic values like LoadBalancer IPs, Service endpoints, or ConfigMap data for use in other resources.
---

# Data Source: k8sconnect_manifest

Reads an existing Kubernetes resource from the cluster and makes its data available to Terraform configuration. Use this to reference cluster resources not managed by Terraform (e.g., cloud provider defaults, operator-created resources) or to access dynamic values like LoadBalancer IPs, Service endpoints, or ConfigMap data for use in other resources.

## Example Usage - Reading Cluster Resources

```terraform
# Read the kubernetes API server service (present in all clusters)
data "k8sconnect_manifest" "kubernetes_api" {
  api_version = "v1"
  kind        = "Service"
  name        = "kubernetes"
  namespace   = "default"

  cluster_connection = var.cluster_connection
}

# Parse the manifest JSON to access fields
locals {
  kubernetes_api = jsondecode(data.k8sconnect_manifest.kubernetes_api.manifest)
}

# Use the data in other resources
resource "k8sconnect_manifest" "api_endpoint_config" {
  yaml_body = <<-YAML
    apiVersion: v1
    kind: ConfigMap
    metadata:
      name: api-info
      namespace: default
    data:
      cluster_ip: "${local.kubernetes_api.spec.clusterIP}"
      port: "${tostring(local.kubernetes_api.spec.ports[0].port)}"
      endpoint: "https://${local.kubernetes_api.spec.clusterIP}:${tostring(local.kubernetes_api.spec.ports[0].port)}"
  YAML

  cluster_connection = var.cluster_connection
}
```

## Example Usage - Reading Cloud Provider Resources

Read resources created by cloud providers (EKS, GKE, AKS):

```terraform
# Read AWS EKS aws-node DaemonSet configuration
data "k8sconnect_manifest" "aws_node" {
  api_version = "apps/v1"
  kind        = "DaemonSet"
  name        = "aws-node"
  namespace   = "kube-system"

  cluster_connection = {
    host                   = aws_eks_cluster.main.endpoint
    cluster_ca_certificate = base64decode(aws_eks_cluster.main.certificate_authority[0].data)
    exec = {
      api_version = "client.authentication.k8s.io/v1"
      command     = "aws"
      args        = ["eks", "get-token", "--cluster-name", aws_eks_cluster.main.name]
    }
  }
}

locals {
  aws_node = jsondecode(data.k8sconnect_manifest.aws_node.manifest)
}

output "aws_node_image" {
  value       = local.aws_node.spec.template.spec.containers[0].image
  description = "Current AWS VPC CNI image version"
}
```

## Accessing Field Data

The datasource provides three output formats:

- `manifest` - JSON string representation (use with `jsondecode()`)
- `yaml_body` - YAML string representation
- `object` - Dynamic object (currently null, use `manifest` with `jsondecode()` instead)

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `api_version` (String) API version of the resource (e.g., 'v1', 'apps/v1')
- `cluster_connection` (Attributes) Cluster connection configuration (see [below for nested schema](#nestedatt--cluster_connection))
- `kind` (String) Kind of the resource (e.g., 'ConfigMap', 'Deployment')
- `name` (String) Name of the resource

### Optional

- `namespace` (String) Namespace of the resource (optional for cluster-scoped resources, defaults to 'default' for namespaced resources if not specified)

### Read-Only

- `manifest` (String) JSON representation of the complete resource
- `object` (Dynamic) The resource object for accessing individual fields
- `yaml_body` (String) YAML representation of the complete resource

<a id="nestedatt--cluster_connection"></a>
### Nested Schema for `cluster_connection`

Optional:

- `client_certificate` (String, Sensitive) Client certificate for TLS authentication. Accepts PEM format or base64-encoded PEM - automatically detected.
- `client_key` (String, Sensitive) Client certificate key for TLS authentication. Accepts PEM format or base64-encoded PEM - automatically detected.
- `cluster_ca_certificate` (String, Sensitive) Root certificate bundle for TLS authentication. Accepts PEM format or base64-encoded PEM - automatically detected.
- `context` (String) Context to use from the kubeconfig. Optional when kubeconfig contains exactly one context (that context will be used automatically). Required when kubeconfig contains multiple contexts to prevent accidental connection to the wrong cluster. Error will list available contexts if not specified when required.
- `exec` (Attributes, Sensitive) Configuration for exec-based authentication. (see [below for nested schema](#nestedatt--cluster_connection--exec))
- `host` (String) The hostname (in form of URI) of the Kubernetes API server.
- `insecure` (Boolean) Whether server should be accessed without verifying the TLS certificate.
- `kubeconfig` (String, Sensitive) Raw kubeconfig file content.
- `proxy_url` (String) URL of the proxy to use for requests.
- `token` (String, Sensitive) Token to authenticate to the Kubernetes API server.

<a id="nestedatt--cluster_connection--exec"></a>
### Nested Schema for `cluster_connection.exec`

Required:

- `api_version` (String) API version to use when encoding the ExecCredentials resource.
- `command` (String) Command to execute.

Optional:

- `args` (List of String) Arguments to pass when executing the plugin.
- `env` (Map of String) Environment variables to set when executing the plugin.
